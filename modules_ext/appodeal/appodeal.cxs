Strict

' Appodeal module by Rich Hanson
' Use at your own risk
' All code here on Github: https://github.com/pantson/pantson-cerberusx-appodeal

#If TARGET<>"android"
#Error "The Appodeal module is only available on the Android target"
#End

#If TARGET="android"	
	Import "native/appodeal.android.java"

	#If ANDROID_MIN_SDK_VERSION < 28
		#ANDROID_MIN_SDK_VERSION = 28
	#End

	#If ANDROID_TARGET_SDK_VERSION < 29
		#ANDROID_TARGET_SDK_VERSION=29
	#End
	
	#If ANDROID_BUILD_TOOLS_VERSION
	#If ANDROID_BUILD_TOOLS_VERSION<29.0.2
		#ANDROID_BUILD_TOOLS_VERSION=29.0.2
	#End
	#end

	#SRCS+=";${CD}/app/src/main/res/xml/network_security_config.xml;"

	#ANDROID_LIBRARY_REFERENCE_2+="implementation ('com.appodeal.ads:sdk:2.8.1.+') {
      exclude group: 'com.appodeal.ads.sdk.networks', module: 'ironsource'~n"

	#If APPODEAL_AMAZON
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: 'ogury'"
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: 'yandex'"
	#End

	#If Not ADMOB_APP_ID
		#If CONFIG="debug"
			#Print("#ADMOB_APP_ID not defined. Get this from the Admob dashboard. Omitting Admob module from Appodeal plugin")
		#End
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: 'admob'~n"
	#Else
		#ANDROID_MANIFEST_APPLICATION+="<meta-data android:name=~qcom.google.android.gms.ads.APPLICATION_ID~q android:value=~q${ADMOB_APP_ID}~q />~n"
	#End
	#If APPODEAL_EXCLUDE_1
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: '"+#APPODEAL_EXCLUDE_1+"'~n"
	#End
	#If APPODEAL_EXCLUDE_2
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: '"+#APPODEAL_EXCLUDE_2+"'~n"
	#End
	#If APPODEAL_EXCLUDE_3
		#ANDROID_LIBRARY_REFERENCE_2+="      exclude group: 'com.appodeal.ads.sdk.networks', module: '"+#APPODEAL_EXCLUDE_3+"'~n"
	#End

	#ANDROID_LIBRARY_REFERENCE_2+="    }~n    implementation 'com.explorestack:consent:1.0.1'~n"

	#ANDROID_GRADLE_VERSION="3.5.4"
	#ANDROID_GRADLE_DISTRIBUTION="gradle-6.1.1-all.zip"
	#ANDROID_APPLICATION_EXTRAS+="android:networkSecurityConfig=~q@xml/network_security_config~q~n"
	#ANDROID_REPOSITORIES+="maven{url ~qhttps://artifactory.appodeal.com/appodeal~q}~n"

	#ANDROID_MANIFEST_MAIN+="<uses-permission android:name=~qandroid.permission.ACCESS_NETWORK_STATE~q />
	<uses-permission android:name=~qandroid.permission.INTERNET~q />"
    
#If APPODEAL_LOCATION_PERMISSIONS
	#ANDROID_MANIFEST_MAIN+="	<uses-permission android:name=~qandroid.permission.ACCESS_COARSE_LOCATION~q />
	<uses-permission android:name=~qandroid.permission.ACCESS_FINE_LOCATION~q />
	<uses-permission android:name=~qandroid.permission.WRITE_EXTERNAL_STORAGE~q />"
	#End

#If APPODEAL_FULL_PERMISSIONS
	#ANDROID_MANIFEST_MAIN+="	<uses-permission android:name=~qandroid.permission.ACCESS_COARSE_LOCATION~q />
	<uses-permission android:name=~qandroid.permission.ACCESS_FINE_LOCATION~q />
	<uses-permission android:name=~qandroid.permission.WRITE_EXTERNAL_STORAGE~q />
	<uses-permission android:name=~qandroid.permission.ACCESS_WIFI_STATE~q /><!--optional-->
	<uses-permission android:name=~qandroid.permission.VIBRATE~q /><!--optional-->"
#End

	#ANDROID_MANIFEST_APPLICATION+="<uses-library android:name=~qorg.apache.http.legacy~q android:required=~qfalse~q />~n"
#End 

Extern

Class _appodeal Extends Null="BBAppodeal"
	Method initialise:Void(appKey:String,adType:Int)
	Method show:Void(adType:Int)
	Method hide:Void(adType:Int)
	Method isLoaded:Bool(adType:Int)
	Method getGDPRconsent:Bool()
	Method enableLogging:Void()
	Method disableLogging:Void()
	Method enableVerboseLogging:Void()
	Method setTesting:Void(state:Bool)
	Method updateGDPRconsent:Void(value:Bool)
	Method updateCOPPA:Void(value:Bool)
	Method ignoreCOPPA:Void()
End

Public
Class Appodeal
	Const INTERSTITIAL:=3
	Const BANNER:=4
	Const BANNERBOTTOM:=8
	Const BANNERTOP:=16
	Const REWARDED_VIDEO:=128
	Const NON_SKIPPABLE_VIDEO:=128
	Const MREC:=256
	Const Nativ:=512
	
	Const ALL:=1023

	Field _ad:_appodeal
	
	Method New()
		_ad = New _appodeal
	End
	
	Method Initialise:Void(AppKey:String,AdTypes:Int=1023)
		_ad.initialise(AppKey,AdTypes)
#If CONFIG="debug"
		_ad.setTesting(True)
		_ad.enableLogging()
#End
		EnableCOPPA()
	End
	
	Method IsLoaded:Bool(AdType:Int)
		Return _ad.isLoaded(AdType)
	End
	
	Method Show:Void(AdType:Int)
		_ad.show(AdType)
	End
	
	Method Hide:Void(AdType:Int)
		_ad.hide(AdType)
	End

	Method GetGDPRConsent:Bool()
		Return _ad.getGDPRconsent()
	End

	Method EnableGDPRConsent:Void()
		_ad.updateGDPRconsent(True)
	End

	Method DisableGDPRConsent:Void()
		_ad.updateGDPRconsent(False)
	End
	
	Method IgnoreCOPPA:Void()
		_ad.ignoreCOPPA()
	End
	
	Method EnableCOPPA:Void()
		_ad.updateCOPPA(True)
	End
	
	Method DisableCOPPA:Void()
		_ad.updateCOPPA(False)
	End
	
	Method EnableTestAds:Void()
		_ad.setTesting(True)
	End

	Method DisableTestAds:Void()
		_ad.setTesting(False)
	End
	
	Method DisableLogging:Void()
		_ad.disableLogging()
	End
	
	Method EnableLogging:Void()
		_ad.enableLogging()
	End
	
	Method EnableVerboseLogging:Void()
		_ad.enableVerboseLogging()
	End
End
